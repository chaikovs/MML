! DATE:   10/24/05
! The East chicane is turned off and the East and West IR quads and chicane
! drifts are readjusted to maintain the 1.6 m double waist in the East IR 
! and 2.5 m low beta in the East and West matching cells. The West IR phase 
! is matched to the East phase. 
! Based on deck "sp3v81f_dwc.14.13-6.22.symm.mad".

! DATE:   09/09/04
! The left/right quad K-values in the East pit are made more symmetric
! then in "sp3v81f_dwc.14.13-6.22.mad" by constraining the right-left 
! strength difference in the MAD match.
!
! DATE:   08/03/04
! Asymmetric chicane in the East matching section with double waist 
! low beta-y (1.6 m), and low beta-y (2.5 m) in all 4.8 m straights.
! Chicane deflects to INside the ring.
! Tune: 14.13 / 6.22, East/West sections phase: 1.6 / 1.34 [2pi].
! Emittance = 18.3 nm.
! Linear chromaticity is set to +1.
! Sextupoles may need optimization.
!
! All the lengths of quadrupoles were changed to effective lengths. 03/13/03
!
! SPEAR3 V8.1 Positions of bpms and correctors were
! updated by Jeff in accordance with the drawings.
! Lengths of the dipole magnets were changed to the effective lengths.
! Accordingly the gradients of the dipoles were also changed. *** by M. Yoon
! 03/12/2003
!
! SPEAR 3 Lattice SP3V81
! DATE AND TIME:   06/21/02  08.00.00
!
! FILE:            SP3V81RESP.MAD
! From SP3V80.MAD with SFI and QDX moves
! Includes correctors and BPMs

! ID source points

 BL4:  MARKER
 BL5:  MARKER
 BL6:  MARKER
 BL7:  MARKER
 BL9:  MARKER
 BL10: MARKER
 BL11: MARKER
 BL12: MARKER
 BL13: MARKER

! BPM

 BPM: MONITOR
 
! CORRECTOR

 HCOR: HKICKER, L=0.0
 VCOR: VKICKER, L=0.0
 DCOR: DRIFT,   L=0.075

 HVCOR: LINE=(DCOR,HCOR,VCOR,DCOR)

! RF-cavity

 RFVOLT := 0.8
 CAVITY: RFCAVITY, L=1.50, VOLT=RFVOLT, LAG=0.5, &
                   HARMON=372, SHUNT=0.5, TFILL=25.0

! Standard Cell Drifts 

 DC1A: DRIFT, L=1.405934
 DC1B: DRIFT, L=0.12404125
 DC2:  DRIFT, L=0.0975
 DC3:  DRIFT, L=0.25
 DC4:  DRIFT, L=0.23
 DC5:  DRIFT, L=0.200986
 DC6:  DRIFT, L=0.18
 
! Standard Cell BPM Drifts

 DC3A:  DRIFT, L=0.05322065
 DC3B:  DRIFT, L=0.16368247
 DC4A:  DRIFT, L=0.15921467
 DC4B:  DRIFT, L=0.044418
 DC6A:  DRIFT, L=0.110646   
 DC6B:  DRIFT, L=0.06316585

! Standard Cell Corrector Drifts

 DC2A:  DRIFT, L=0.11576525 
 DC2B:  DRIFT, L=0.11581045
 DC2C:  DRIFT, L=0.10210045 
 DC2D:  DRIFT, L=0.12947525 
 DC5A:  DRIFT, L=0.09058  
 DC5B:  DRIFT, L=0.36139 
 DC5C:  DRIFT, L=0.09584 
 DC5D:  DRIFT, L=0.35613

! Standard Cell Dipoles change the length to effective length

 BEND: SBEND, L=1.5048, K1=-0.31537858, ANGLE=0.184799567858223 ,&  
              E1=0.092399783929112 , E2=0.092399783929112 

! Standard Cell Quadrupoles

 QF:  QUADRUPOLE, L=0.3533895, K1= 1.753130510678
 QD:  QUADRUPOLE, L=0.1634591, K1=-1.503519522518
 QFC: QUADRUPOLE, L=0.5123803, K1= 1.748641980699

! Standard Cell Sextupoles for +1 linear chromaticity

SF: SEXTUPOLE, L=0.21, K2=32.791744877727
SD: SEXTUPOLE, L=0.25, K2=-40.152203017035

! Standard Cell Definitions

 HCEL1: LINE=(DC1A, BPM, DC1B, QF, DC2A, HVCOR, DC2B, QD, DC3A, BPM,&  
              DC3B, BEND, DC4A, BPM, DC4B, SD,DC5A, HVCOR, DC5B, SF,&  
              DC6A, BPM, DC6B, QFC)

 HCEL2: LINE=(DC6B, DC6A, SF, DC5C, HVCOR, DC5D, SD, DC4B, BPM, DC4A,&
              BEND, DC3B, DC3A, QD, DC2C, HVCOR, DC2D, QF, DC1B, BPM, DC1A)

 CEL: LINE=(HCEL1,HCEL2)

 CEL3: LINE=(CEL)
 CEL4: LINE=(CEL)
 CEL5: LINE=(CEL)
 CEL6: LINE=(CEL)
 CEL7: LINE=(CEL)
 CEL12: LINE=(CEL)
 CEL13: LINE=(CEL)
 CEL14: LINE=(CEL)
 CEL15: LINE=(CEL)
 CEL16: LINE=(CEL)

! Cells adjacent to matching cells with special quads for low beta-y

 QFM2E: QUADRUPOLE, L=0.3533895, K1= 1.649475379921
 QDM2E: QUADRUPOLE, L=0.1634591, K1=-1.355564898126
 QDM1E: QUADRUPOLE, L=0.1634591, K1=-1.929807607945
 QFM1E: QUADRUPOLE, L=0.3533895, K1= 1.779734740786

 QFM2W: QUADRUPOLE, L=0.3533895, K1= QFM2E[K1]
 QDM2W: QUADRUPOLE, L=0.1634591, K1= QDM2E[K1]
 QDM1W: QUADRUPOLE, L=0.1634591, K1= QDM1E[K1]
 QFM1W: QUADRUPOLE, L=0.3533895, K1= QFM1E[K1]

 CEL2: LINE=(DC1A, BPM, DC1B, QFM1W, DC2A, HVCOR, DC2B, QDM1W, DC3A, BPM,&  
             DC3B, BEND, DC4A, BPM, DC4B, SD,DC5A, HVCOR, DC5B, SF,&  
             DC6A, BPM, DC6B, QFC, DC6B, DC6A, SF, DC5C, HVCOR, DC5D,&
             SD, DC4B, BPM, DC4A, BEND, DC3B, DC3A, QDM2W, DC2C, HVCOR,&
             DC2D, QFM2W, DC1B, BPM, DC1A)
 CEL8: LINE=(DC1A, BPM, DC1B, QFM2E, DC2A, HVCOR, DC2B, QDM2E, DC3A, BPM,&  
             DC3B, BEND, DC4A, BPM, DC4B, SD,DC5A, HVCOR, DC5B, SF,&  
             DC6A, BPM, DC6B, QFC, DC6B, DC6A, SF, DC5C, HVCOR, DC5D,&
             SD, DC4B, BPM, DC4A, BEND, DC3B, DC3A, QDM1E, DC2C, HVCOR,&
             DC2D, QFM1E, DC1B, BPM, DC1A)
 CEL11: LINE=(DC1A, BPM, DC1B, QFM1E, DC2A, HVCOR, DC2B, QDM1E, DC3A, BPM,&  
              DC3B, BEND, DC4A, BPM, DC4B, SD,DC5A, HVCOR, DC5B, SF,&  
              DC6A, BPM, DC6B, QFC, DC6B, DC6A, SF, DC5C, HVCOR, DC5D,&
              SD, DC4B, BPM, DC4A, BEND, DC3B, DC3A, QDM2E, DC2C, HVCOR,&
              DC2D, QFM2E, DC1B, BPM, DC1A)
 CEL17: LINE=(DC1A, BPM, DC1B, QFM2W, DC2A, HVCOR, DC2B, QDM2W, DC3A, BPM,&  
              DC3B, BEND, DC4A, BPM, DC4B, SD,DC5A, HVCOR, DC5B, SF,&  
              DC6A, BPM, DC6B, QFC, DC6B, DC6A, SF, DC5C, HVCOR, DC5D,&
              SD, DC4B, BPM, DC4A, BEND, DC3B, DC3A, QDM1W, DC2C, HVCOR,&
              DC2D, QFM1W, DC1B, BPM, DC1A)

! Matching Cell Drifts without correctors or BPMs

 DM1: DRIFT, L=3.810000
 DM2: DRIFT, L=0.0975
 DM3: DRIFT, L=0.275
 DM4: DRIFT, L=0.21584572
 DM5: DRIFT, L=0.25
 DM6: DRIFT, L=0.490684631
 DM7: DRIFT, L=0.17380985
 DM8: DRIFT, L=0.5
 DM9: DRIFT, L=0.105
 DM10: DRIFT, L=3.27657140

! Matching Cell A BPM Drifts

 DA1A: DRIFT, L=3.6792386
 DA1B: DRIFT, L=0.12406665
 DA3A: DRIFT, L=0.20889925
 DA3B: DRIFT, L=0.05414045
 DA5A: DRIFT, L=0.11397747
 DA5B: DRIFT, L=0.108563
 DA5C: DRIFT, L=0.051845
 DA5D: DRIFT, L=0.17069547
 DA7A: DRIFT, L=0.1106966
 DA7B: DRIFT, L=0.06311325
 DA8A: DRIFT, L=0.33735947
 DA8B: DRIFT, L=0.12848625
 DA10A: DRIFT, L=0.12393965
 DA10B: DRIFT, L=2.2759863
 DA10C: DRIFT, L=0.8699507
 DA1AW: DRIFT, L=0.6792386
 DA1AE: DRIFT, L=0.208819075709

! Matching Cell A Corrector Drifts

 DA2A: DRIFT, L=0.11530525
 DA2B: DRIFT, L=0.11773445
 DA6A: DRIFT, L=0.126600
 DA6B: DRIFT, L=0.904768280
 DA6C: DRIFT, L=0.096000
 DA6D: DRIFT, L=0.93537
 DA9A: DRIFT, L=0.10930525
 DA9B: DRIFT, L=0.13730525

! Matching Cell B  BPM Drifts 

 DB1A: DRIFT, L=3.747082
 DB1B: DRIFT, L=0.05622325
 DB3A: DRIFT, L=0.13222685
 DB3B: DRIFT, L=0.13081285
 DB5A: DRIFT, L=0.17069547
 DB5B: DRIFT, L=0.051845
 DB5C: DRIFT, L=0.1085632
 DB5D: DRIFT, L=0.11397727
 DB7A: DRIFT, L=0.06311305
 DB7B: DRIFT, L=0.1106968
 DB8A: DRIFT, L=0.32725027
 DB8B: DRIFT, L=0.13859545
 DB10A: DRIFT, L=0.12404125
 DB10B: DRIFT, L=2.2758847
 DB10C: DRIFT, L=0.8699507
 DB1AW: DRIFT, L=0.7470820
 DB1AE: DRIFT, L=0.307108990224

! Matching Cell B Corrector Drifts

 DB2A: DRIFT, L=0.11580525
 DB2B: DRIFT, L=0.11723445
 DB6A: DRIFT, L=0.937370
 DB6B: DRIFT, L=0.093998520
 DB6C: DRIFT, L=0.90437000
 DB6D: DRIFT, L=0.127000000
 DB9A: DRIFT, L=0.12330525
 DB9B: DRIFT, L=0.12330525 

! Matching Cell Dipoles

 B34: SBEND, L=1.14329, ANGLE=0.138599675893667 , K1=-0.31537858, &  
             E1=0.069299837946834 , E2=0.069299837946834 

! Matching Cell Quadrupoles

 QDXW: QUADRUPOLE, L=0.3533895, K1=-1.196087605854
 QFXW: QUADRUPOLE, L=0.6105311, K1= 1.746509342498
 QDYW: QUADRUPOLE, L=0.3533895, K1=-1.253618614328
 QFYW: QUADRUPOLE, L=0.5123803, K1= 1.481494879648
 QDZW: QUADRUPOLE, L=0.3533895, K1=-1.366619537287
 QFZW: QUADRUPOLE, L=0.3533895, K1= 1.698171945583

QDXBE: QUADRUPOLE, L=0.3533895, K1=-0.546390688493
QFXBE: QUADRUPOLE, L=0.6105311, K1=1.416035854808
QDYBE: QUADRUPOLE, L=0.3533895, K1=-1.318512587529
QFYBE: QUADRUPOLE, L=0.5123803, K1=1.481494867281
QDZBE: QUADRUPOLE, L=0.3533895, K1=-0.964648123194
QFZBE: QUADRUPOLE, L=0.3533895, K1=1.614715367608

 QDXAE: QUADRUPOLE, L=0.3533895, K1= QDXBE[K1]
 QFXAE: QUADRUPOLE, L=0.6105311, K1= QFXBE[K1]
 QDYAE: QUADRUPOLE, L=0.3533895, K1= QDYBE[K1]
 QFYAE: QUADRUPOLE, L=0.5123803, K1= QFYBE[K1]
 QDZAE: QUADRUPOLE, L=0.3533895, K1= QDZBE[K1]
 QFZAE: QUADRUPOLE, L=0.3533895, K1= QFZBE[K1]

QDE: QUADRUPOLE, L=0.30526555, K1=-1.987194142639
QFBE: QUADRUPOLE, L=0.3533895, K1=1.434738170728
QFAE: QUADRUPOLE, L=0.3533895, K1=QFBE[K1]

! Matching Cell Sextupoles

 SFI: SEXTUPOLE, L=0.21, K2= 15.38357466721
 SDI: SEXTUPOLE, L=0.21, K2=-19.003795311486

! Matching Cell Markers

 MMM:   MARKER  !...MARKER FOR LEGO TRACKING STARTING POINT
 MIPAE: MARKER  !...MARKER AT DOUBLE WAIST
 MIPBE: MARKER  !...MARKER AT DOUBLE WAIST
 MMW:   MARKER  !...MARKER AT CENTER OF 4.8M WEST STRAIGHT
 MME:   MARKER  !...MARKER AT CENTER OF 4.8M EAST STRAIGHT
 MRF:   MARKER  !...MARKER AT MID-RF 

! West matching cells with RF cavities

 CEL1: LINE=(CAVITY, CAVITY, DA1AW, BPM, DA1B, QDXW, DA2A, HVCOR, DA2B,&
             QFXW, DA3A, BPM, DA3B, QDYW, DM4, B34, DA5A, BPM, DA5B,&
             SDI, DA6A, HVCOR, DA6B, SFI, DA7A ,BPM ,DA7B ,QFYW,&
             DM7 ,SFI ,DA6C, HVCOR, DA6D, SDI, DA5C, BPM, DA5D, B34,&
             DA8A, BPM, DA8B, QDZW, DA9A, HVCOR, DA9B, QFZW, DA10A, BPM,&
             DA10B, MMW, DA10C)
 
! CEL18 is in reversed order

 CEL18: LINE=(CAVITY, CAVITY, DB1AW, BPM, DB1B, QDXW, DB2A, HVCOR, DB2B,&
              QFXW, DB3A, BPM, DB3B, QDYW, DM4, B34, DB5A, BPM, DB5B,& 
              SDI, DB6A, HVCOR, DB6B, SFI, DM7, QFYW, DB7A, BPM,&
              DB7B, SFI, DB6C, HVCOR, DB6D, SDI, DB5C, BPM, DB5D, B34,&  
              DB8A, BPM, DB8B, QDZW, DB9A, HVCOR, DB9B, QFZW, DB10A, BPM,&
              DB10B, MMW, DB10C)

! Chicane bend straight lengths

 LB1ES := 0.08
 LB2ES := 0.15
 LB3ES := 0.16
 LB4ES := 0.10

! Chicane bending angles = 0

 AB1E := 0
 AB2E := 0
 AB3E := 0
 AB4E := 0

! Chicane beam path lengths

 LB1E := LB1ES
 LB2E := LB2ES
 LB3E := LB3ES
 LB4E := LB4ES

 B1E: SBEND, L=LB1E, ANGLE=AB1E, E1=AB1E/2, E2=AB1E/2
 B2E: SBEND, L=LB2E, ANGLE=AB2E, E1=AB2E/2, E2=AB2E/2
 B3E: SBEND, L=LB3E, ANGLE=AB3E, E1=AB3E/2, E2=AB3E/2
 B4E: SBEND, L=LB4E, ANGLE=AB4E, E1=AB4E/2, E2=AB4E/2

! Chicane drifts (adjusted for symmetric position of double waist)

 DCH1A: DRIFT, L=0.2380247545
 DCH1B: DRIFT, L=0.2380247545
 DCH2A: DRIFT, L=0.1080751842482
 DCH2B: DRIFT, L=0.12852218657761
 DCH3A: DRIFT, L=1.1028323931205
 DCH3B: DRIFT, L=1.0923853905

! CEL9 is in reversed order

 CEL9: LINE=(QDE, DCH1B, QFBE, DCH2B, B2E, DCH3B, MIPBE, DCH3B, B1E,&
             DB1AE, BPM, DB1B, QDXBE, DB2A, HVCOR, DB2B,&
             QFXBE, DB3A, BPM, DB3B, QDYBE, DM4, B34, DB5A, BPM, DB5B,& 
             SDI, DB6A, HVCOR, DB6B, SFI, DM7, QFYBE, DB7A, BPM,&
             DB7B, SFI, DB6C, HVCOR, DB6D, SDI, DB5C, BPM, DB5D, B34,&  
             DB8A, BPM, DB8B, QDZBE, DB9A, HVCOR, DB9B, QFZBE, DB10A, BPM,&
             DB10B, MME, DB10C)

 CEL10: LINE=(QDE, DCH1A, QFAE, DCH2A, B3E, DCH3A, MIPAE, DCH3A, B4E,&
              DA1AE, BPM, DA1B, QDXAE, DA2A, HVCOR, DA2B,&
              QFXAE, DA3A, BPM, DA3B, QDYAE, DM4, B34, DA5A, BPM, DA5B,&
              SDI, DA6A, HVCOR, DA6B, SFI, DA7A ,BPM ,DA7B ,QFYAE,&
              DM7 ,SFI ,DA6C, HVCOR, DA6D, SDI, DA5C, BPM, DA5D, B34,&
              DA8A, BPM, DA8B, QDZAE, DA9A, HVCOR, DA9B, QFZAE, DA10A, BPM,&
              DA10B, MME, DA10C)
 
! Full ring

 RING: LINE= (MRF,CEL1,MMM,&
   BL13,CEL2,BL12,CEL3,BL11,CEL4,CEL5,BL4,CEL6,BL5,CEL7,BL6,CEL8,-CEL9,&
   CEL10,CEL11,BL9,CEL12,BL10,CEL13,CEL14,CEL15,CEL16,CEL17,-CEL18)


 RETURN




!---------------------------------------------------------------------------
! Matching routines

!------------------------------------------------------------------
! Matching conditions

 bxirw := 5.0
 byirw := 10.0

 bxire := 8.7308
 byire := 1.6

 bxmw := 12.4544
 bymw := 2.5

 bxme := 12.4544
 byme := 2.5

! For tune 14.13 / 6.22 and Symmetric East/West phase

 muxdes := 14.13
 muydes :=  6.22
 muxire :=  1.6
 muyire :=  1.34
 muxirw :=  muxire
 muyirw :=  muyire

 muxcell := (muxdes - muxirw - muxire) / 14
 muycell := (muydes - muyirw - muyire) / 14

 muxarc := 14 * muxcell
 muyarc := 14 * muycell

 K1max := 2.0

!---------------------------------------------------------------------------
! To match a standard cell

 MCELL: subroutine
  use, CEL
   cell
 !  vary, QFC[K1], step=0.01
   vary, QD[K1], step=0.01
   vary, QF[K1], step=0.01
    constr, #e, mux=muxcell, muy=muycell
 !   constr, #e, dx=0 dpx=0
 !   constr, #e, dx=0.1 dpx=0
   lmdif, calls=2000, tolerance=1e-20
   endmatch
   twiss couple
 endsubroutine

!--------------------------------------------------------------------------- 
! To match symmetric East section without chicane
 
 MIRENOCH: subroutine
  use (CEL8,-CEL9)
   match, line=CEL, orbit

   vary, QFYBE[K1], step=0.01 ! lower=0
   constr, #e, dx=0, dpx=0

   vary, QFM1E[K1], step=0.01  lower=0  upper= K1max
   vary, QDM1E[K1], step=0.01  upper=0  lower=-K1max
   vary, QFM2E[K1], step=0.01  lower=0  upper= K1max
   vary, QDM2E[K1], step=0.01  upper=0  lower=-K1max
   vary, QFZBE[K1], step=0.01 ! lower=0
   vary, QDZBE[K1], step=0.01 ! upper=0
   vary, QDYBE[K1], step=0.01 ! upper=0
   vary, QFXBE[K1], step=0.01 ! lower=0
   vary, QDXBE[K1], step=0.01 ! upper=0
   vary, QFBE[K1] , step=0.01  lower=0  upper= K1max
   vary, QDE[K1]  , step=0.01  upper=0  lower=-K1max

   constr, #e, alfx=0, alfy=0
   constr, #e, mux=muxcell+muxire/2, muy=muycell+muyire/2

   constr, MME[1]  , alfx=0, alfy=0, bety=byme
   constr, MIPBE[1], alfx=0, alfy=0, bety=byire

   weight, betx=1e-6
   constr, MME[1]  , betx=bxme
   constr, MIPBE[1], betx=bxire

   weight, betx=1e-6, bety=1e-6
   constr, class=quadrupole, betx<16, bety<16
   constr, class=sbend     , betx<16, bety<16

   lmdif, calls=2000, tolerance=1e-20
   endmatch
 endsubroutine
!---------------------------------------------------------------------------
! To match symmetric West section

 MIRWNOCH: subroutine
  use (CEL17,-CEL18)
   match line=CEL, orbit

   vary, QFZW[K1], step=0.01, lower=0.
   vary, QDZW[K1], step=0.01, upper=0.
   vary, QFYW[K1], step=0.01, lower=0.
   vary, QDYW[K1], step=0.01, upper=0.
   vary, QFXW[K1], step=0.01, lower=0.
   vary, QDXW[K1], step=0.01, upper=0.

   constr, #e, dx=0, dpx=0
   constr, #e, alfx=0, alfy=0
   constr, #e, mux=muxcell+muxirw/2, muy=muycell+muyirw/2

   weight, betx=1e-6, bety=1e-6
   constr, #e, betx=bxirw, bety<byirw

   weight, betx=1e-6, bety=1e-6
   constr, class=quadrupole, betx<15, bety<15
   constr, class=sbend     , betx<15, bety<15

   lmdif, calls=2000, tolerance=1e-20
   endmatch
   twiss couple line=CEL
 endsubroutine
!---------------------------------------------------------------------------
 PLOTRING: subroutine
  set RFVOLT (0.0)
  use RING
  survey
  print full
  twiss couple save
  plot title= &
  "SPEAR-3 with double waist in the East IR." &
  haxis=s vaxis1=betx,bety, style=100 colour=100 spline=.T. vmin=0 vmax=20
  plot title= &
  "SPEAR-3 with double waist in the East IR." &
  haxis=s vaxis=dx, style=100 colour=100 spline=.T. vmin=-0.1 vmax=0.5
 endsubroutine
!---------------------------------------------------------------------------

 set RFVOLT (0.0)
! MCELL
! MIRENOCH
! MIRWNOCH
 PLOTRING
 SAVE

 STOP
