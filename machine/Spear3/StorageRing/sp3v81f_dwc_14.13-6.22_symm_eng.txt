! DATE:   31/08/06
! Engineering update of double waist chicane bends and optics rematch.
! Total path length = 234.144142 m, tune = 14.13/6.22,
! linear chromaticity = +1/+1, x-emittance = 17.8 nm.
!
! DATE:   09/09/04
! The left/right quad K-values in the East pit are made more symmetric
! then in "sp3v81f_dwc.14.13-6.22.mad" by constraining the right-left 
! strength difference in the MAD match.
!
! DATE:   08/03/04
! Asymmetric chicane in the East matching section with double waist 
! low beta-y (1.6 m), and low beta-y (2.5 m) in all 4.8 m straights.
! Chicane deflects to INside the ring.
! Tune: 14.13 / 6.22, East/West sections phase: 1.6 / 1.34 [2pi].
! Emittance = 18.3 nm.
! Linear chromaticity is set to +1.
! Sextupoles may need optimization.
!
! All the lengths of quadrupoles were changed to effective lengths. 03/13/03
!
! SPEAR3 V8.1 Positions of bpms and correctors were
! updated by Jeff in accordance with the drawings.
! Lengths of the dipole magnets were changed to the effective lengths.
! Accordingly the gradients of the dipoles were also changed. *** by M. Yoon
! 03/12/2003
!
! SPEAR 3 Lattice SP3V81
! DATE AND TIME:   06/21/02  08.00.00
!
! FILE:            SP3V81RESP.MAD
! From SP3V80.MAD with SFI and QDX moves
! Includes correctors and BPMs

! ID source points

 BL4:  MARKER
 BL5:  MARKER
 BL6:  MARKER
 BL7:  MARKER
 BL9:  MARKER
 BL10: MARKER
 BL11: MARKER
 BL12: MARKER
 BL13: MARKER

! BPM

 BPM: MONITOR
 
! CORRECTOR

 HCOR: HKICKER, L=0.0
 VCOR: VKICKER, L=0.0
 DCOR: DRIFT,   L=0.075

 HVCOR: LINE=(DCOR,HCOR,VCOR,DCOR)

! RF-cavity

 RFVOLT := 0.8
 CAVITY: RFCAVITY, L=1.50, VOLT=RFVOLT, LAG=0.5, &
                   HARMON=372, SHUNT=0.5, TFILL=25.0

! Standard Cell Drifts 

 DC1A: DRIFT, L=1.405934
 DC1B: DRIFT, L=0.12404125
 DC2:  DRIFT, L=0.0975
 DC3:  DRIFT, L=0.25
 DC4:  DRIFT, L=0.23
 DC5:  DRIFT, L=0.200986
 DC6:  DRIFT, L=0.18
 
! Standard Cell BPM Drifts

 DC3A:  DRIFT, L=0.05322065
 DC3B:  DRIFT, L=0.16368247
 DC4A:  DRIFT, L=0.15921467
 DC4B:  DRIFT, L=0.044418
 DC6A:  DRIFT, L=0.110646   
 DC6B:  DRIFT, L=0.06316585

! Standard Cell Corrector Drifts

 DC2A:  DRIFT, L=0.11576525 
 DC2B:  DRIFT, L=0.11581045
 DC2C:  DRIFT, L=0.10210045 
 DC2D:  DRIFT, L=0.12947525 
 DC5A:  DRIFT, L=0.09058  
 DC5B:  DRIFT, L=0.36139 
 DC5C:  DRIFT, L=0.09584 
 DC5D:  DRIFT, L=0.35613

! Standard Cell Dipoles change the length to effective length

 BEND: SBEND, L=1.5048, K1=-0.31537858, ANGLE=0.184799567858223 ,&  
              E1=0.092399783929112 , E2=0.092399783929112 

! Standard Cell Quadrupoles

 QF:  QUADRUPOLE, L=0.3533895, K1= 1.753130510678
 QD:  QUADRUPOLE, L=0.1634591, K1=-1.503519522518
 QFC: QUADRUPOLE, L=0.5123803, K1= 1.748641980699

! Standard Cell Sextupoles for +1 linear chromaticity

 SF: SEXTUPOLE, L=0.21, K2= 33.429311793235
 SD: SEXTUPOLE, L=0.25, K2=-40.489612356525

! Standard Cell Definitions

 HCEL1: LINE=(DC1A, BPM, DC1B, QF, DC2A, HVCOR, DC2B, QD, DC3A, BPM,&  
              DC3B, BEND, DC4A, BPM, DC4B, SD,DC5A, HVCOR, DC5B, SF,&  
              DC6A, BPM, DC6B, QFC)

 HCEL2: LINE=(DC6B, DC6A, SF, DC5C, HVCOR, DC5D, SD, DC4B, BPM, DC4A,&
              BEND, DC3B, DC3A, QD, DC2C, HVCOR, DC2D, QF, DC1B, BPM, DC1A)

 CEL: LINE=(HCEL1,HCEL2)

 CEL3: LINE=(CEL)
 CEL4: LINE=(CEL)
 CEL5: LINE=(CEL)
 CEL6: LINE=(CEL)
 CEL7: LINE=(CEL)
 CEL12: LINE=(CEL)
 CEL13: LINE=(CEL)
 CEL14: LINE=(CEL)
 CEL15: LINE=(CEL)
 CEL16: LINE=(CEL)

! Cells adjacent to matching cells with special quads for low beta-y

 QFM2E: QUADRUPOLE, L=0.3533895, K1= 1.668556235871
 QDM2E: QUADRUPOLE, L=0.1634591, K1=-1.374597393400
 QDM1E: QUADRUPOLE, L=0.1634591, K1=-1.942744871911
 QFM1E: QUADRUPOLE, L=0.3533895, K1= 1.796107206934

 QFM2W: QUADRUPOLE, L=0.3533895, K1= QFM2E[K1]
 QDM2W: QUADRUPOLE, L=0.1634591, K1= QDM2E[K1]
 QDM1W: QUADRUPOLE, L=0.1634591, K1= QDM1E[K1]
 QFM1W: QUADRUPOLE, L=0.3533895, K1= QFM1E[K1]

 CEL2: LINE=(DC1A, BPM, DC1B, QFM1W, DC2A, HVCOR, DC2B, QDM1W, DC3A, BPM,&  
             DC3B, BEND, DC4A, BPM, DC4B, SD,DC5A, HVCOR, DC5B, SF,&  
             DC6A, BPM, DC6B, QFC, DC6B, DC6A, SF, DC5C, HVCOR, DC5D,&
             SD, DC4B, BPM, DC4A, BEND, DC3B, DC3A, QDM2W, DC2C, HVCOR,&
             DC2D, QFM2W, DC1B, BPM, DC1A)
 CEL8: LINE=(DC1A, BPM, DC1B, QFM2E, DC2A, HVCOR, DC2B, QDM2E, DC3A, BPM,&  
             DC3B, BEND, DC4A, BPM, DC4B, SD,DC5A, HVCOR, DC5B, SF,&  
             DC6A, BPM, DC6B, QFC, DC6B, DC6A, SF, DC5C, HVCOR, DC5D,&
             SD, DC4B, BPM, DC4A, BEND, DC3B, DC3A, QDM1E, DC2C, HVCOR,&
             DC2D, QFM1E, DC1B, BPM, DC1A)
 CEL11: LINE=(DC1A, BPM, DC1B, QFM1E, DC2A, HVCOR, DC2B, QDM1E, DC3A, BPM,&  
              DC3B, BEND, DC4A, BPM, DC4B, SD,DC5A, HVCOR, DC5B, SF,&  
              DC6A, BPM, DC6B, QFC, DC6B, DC6A, SF, DC5C, HVCOR, DC5D,&
              SD, DC4B, BPM, DC4A, BEND, DC3B, DC3A, QDM2E, DC2C, HVCOR,&
              DC2D, QFM2E, DC1B, BPM, DC1A)
 CEL17: LINE=(DC1A, BPM, DC1B, QFM2W, DC2A, HVCOR, DC2B, QDM2W, DC3A, BPM,&  
              DC3B, BEND, DC4A, BPM, DC4B, SD,DC5A, HVCOR, DC5B, SF,&  
              DC6A, BPM, DC6B, QFC, DC6B, DC6A, SF, DC5C, HVCOR, DC5D,&
              SD, DC4B, BPM, DC4A, BEND, DC3B, DC3A, QDM1W, DC2C, HVCOR,&
              DC2D, QFM1W, DC1B, BPM, DC1A)

! Matching Cell Drifts without correctors or BPMs

 DM1: DRIFT, L=3.810000
 DM2: DRIFT, L=0.0975
 DM3: DRIFT, L=0.275
 DM4: DRIFT, L=0.21584572
 DM5: DRIFT, L=0.25
 DM6: DRIFT, L=0.490684631
 DM7: DRIFT, L=0.17380985
 DM8: DRIFT, L=0.5
 DM9: DRIFT, L=0.105
 DM10: DRIFT, L=3.27657140

! Matching Cell A BPM Drifts

 DA1A: DRIFT, L=3.6792386
 DA1B: DRIFT, L=0.12406665
 DA3A: DRIFT, L=0.20889925
 DA3B: DRIFT, L=0.05414045
 DA5A: DRIFT, L=0.11397747
 DA5B: DRIFT, L=0.108563
 DA5C: DRIFT, L=0.051845
 DA5D: DRIFT, L=0.17069547
 DA7A: DRIFT, L=0.1106966
 DA7B: DRIFT, L=0.06311325
 DA8A: DRIFT, L=0.33735947
 DA8B: DRIFT, L=0.12848625
 DA10A: DRIFT, L=0.12393965
 DA10B: DRIFT, L=2.2759863
 DA10C: DRIFT, L=0.8699507
 DA1AW: DRIFT, L=0.6792386
 DA1AE: DRIFT, L=0.099758508543

! Matching Cell A Corrector Drifts

 DA2A: DRIFT, L=0.11530525
 DA2B: DRIFT, L=0.11773445
 DA6A: DRIFT, L=0.126600
 DA6B: DRIFT, L=0.904768280
 DA6C: DRIFT, L=0.096000
 DA6D: DRIFT, L=0.93537
 DA9A: DRIFT, L=0.10930525
 DA9B: DRIFT, L=0.13730525

! Matching Cell B  BPM Drifts 

 DB1A: DRIFT, L=3.747082
 DB1B: DRIFT, L=0.05622325
 DB3A: DRIFT, L=0.13222685
 DB3B: DRIFT, L=0.13081285
 DB5A: DRIFT, L=0.17069547
 DB5B: DRIFT, L=0.051845
 DB5C: DRIFT, L=0.1085632
 DB5D: DRIFT, L=0.11397727
 DB7A: DRIFT, L=0.06311305
 DB7B: DRIFT, L=0.1106968
 DB8A: DRIFT, L=0.32725027
 DB8B: DRIFT, L=0.13859545
 DB10A: DRIFT, L=0.12404125
 DB10B: DRIFT, L=2.2758847
 DB10C: DRIFT, L=0.8699507
 DB1AW: DRIFT, L=0.7470820
 DB1AE: DRIFT, L=0.138081771457

! Matching Cell B Corrector Drifts

 DB2A: DRIFT, L=0.11580525
 DB2B: DRIFT, L=0.11723445
 DB6A: DRIFT, L=0.937370
 DB6B: DRIFT, L=0.093998520
 DB6C: DRIFT, L=0.90437000
 DB6D: DRIFT, L=0.127000000
 DB9A: DRIFT, L=0.12330525
 DB9B: DRIFT, L=0.12330525 

! Matching Cell Dipoles

 B34: SBEND, L=1.14329, ANGLE=0.138599675893667 , K1=-0.31537858, &  
             E1=0.069299837946834 , E2=0.069299837946834 

! Matching Cell Quadrupoles

 QDXW: QUADRUPOLE, L=0.3533895, K1=-1.352137583368
 QFXW: QUADRUPOLE, L=0.6105311, K1= 1.788901294265
 QDYW: QUADRUPOLE, L=0.3533895, K1=-1.228187092157
 QFYW: QUADRUPOLE, L=0.5123803, K1= 1.481494890153
 QDZW: QUADRUPOLE, L=0.3533895, K1=-1.292554906327
 QFZW: QUADRUPOLE, L=0.3533895, K1= 1.652137149352

 QDXBE: QUADRUPOLE, L=0.3533895, K1=-0.506202543201
 QFXBE: QUADRUPOLE, L=0.6105311, K1= 1.393717225884
 QDYBE: QUADRUPOLE, L=0.3533895, K1=-1.316363066520
 QFYBE: QUADRUPOLE, L=0.5123803, K1= 1.487667447513
 QDZBE: QUADRUPOLE, L=0.3533895, K1=-0.971446090856
 QFZBE: QUADRUPOLE, L=0.3533895, K1= 1.623915643638

 QDXAE: QUADRUPOLE, L=0.3533895, K1=-0.498609505069
 QFXAE: QUADRUPOLE, L=0.6105311, K1= 1.375216955766
 QDYAE: QUADRUPOLE, L=0.3533895, K1=-1.304485550061
 QFYAE: QUADRUPOLE, L=0.5123803, K1= 1.479326419997
 QDZAE: QUADRUPOLE, L=0.3533895, K1=-0.957755695355
 QFZAE: QUADRUPOLE, L=0.3533895, K1= 1.596309077780

 QDE:  QUADRUPOLE, L=0.30526555, K1=-1.970309412395
 QFBE: QUADRUPOLE, L=0.3533895,  K1= 1.404889990368
 QFAE: QUADRUPOLE, L=0.3533895,  K1= 1.420293718455

! Matching Cell Sextupoles

 SFI: SEXTUPOLE, L=0.21, K2= 15.38357466721
 SDI: SEXTUPOLE, L=0.21, K2=-19.003795311486

! Markers

 MMM:   MARKER  !...MARKER FOR LEGO TRACKING STARTING POINT
 MIPAE: MARKER  !...MARKER AT DOUBLE WAIST
 MIPBE: MARKER  !...MARKER AT DOUBLE WAIST
 MMW:   MARKER  !...MARKER AT CENTER OF 4.8m WEST STRAIGHT
 MME:   MARKER  !...MARKER AT CENTER OF 4.8m EAST STRAIGHT
 MRF:   MARKER  !...MARKER AT MID-RF 
 M3m:   MARKER  !...MARKER AT CENTER OF 3m STRAIGHT

! West matching cells with RF cavities

 CEL1: LINE=(CAVITY, CAVITY, DA1AW, BPM, DA1B, QDXW, DA2A, HVCOR, DA2B,&
             QFXW, DA3A, BPM, DA3B, QDYW, DM4, B34, DA5A, BPM, DA5B,&
             SDI, DA6A, HVCOR, DA6B, SFI, DA7A ,BPM ,DA7B ,QFYW,&
             DM7 ,SFI ,DA6C, HVCOR, DA6D, SDI, DA5C, BPM, DA5D, B34,&
             DA8A, BPM, DA8B, QDZW, DA9A, HVCOR, DA9B, QFZW, DA10A, BPM,&
             DA10B, MMW, DA10C)
 
! CEL18 is in reversed order

 CEL18: LINE=(CAVITY, CAVITY, DB1AW, BPM, DB1B, QDXW, DB2A, HVCOR, DB2B,&
              QFXW, DB3A, BPM, DB3B, QDYW, DM4, B34, DB5A, BPM, DB5B,& 
              SDI, DB6A, HVCOR, DB6B, SFI, DM7, QFYW, DB7A, BPM,&
              DB7B, SFI, DB6C, HVCOR, DB6D, SDI, DB5C, BPM, DB5D, B34,&  
              DB8A, BPM, DB8B, QDZW, DB9A, HVCOR, DB9B, QFZW, DB10A, BPM,&
              DB10B, MMW, BL13, DB10C)

! Path length in chicane bends

 LB1E := 0.11
 LB2E := 0.11
 LB3E := 0.11
 LB4E := 0.11

! Chicane bending angles

 AB1E :=  0.0020
 AB2E :=  0.0048325
 AB3E := -0.0148325
 AB4E :=  0.0080

 B1E: SBEND, L=LB1E, ANGLE=AB1E, E1=AB1E/2, E2=AB1E/2
 B2E: SBEND, L=LB2E, ANGLE=AB2E, E1=AB2E/2, E2=AB2E/2
 B3E: SBEND, L=LB3E, ANGLE=AB3E, E1=AB3E/2, E2=AB3E/2
 B4E: SBEND, L=LB4E, ANGLE=AB4E, E1=AB4E/2, E2=AB4E/2

! Chicane drifts

 DCH1A: DRIFT, L=0.2380395
 DCH1B: DRIFT, L=0.2380395
 DCH2A: DRIFT, L=0.125619707
 DCH2B: DRIFT, L=0.105489726
 DCH3A: DRIFT, L=1.1686332255
 DCH3B: DRIFT, L=1.193422567

! CEL9 is in reversed order

 CEL9: LINE=(QDE, DCH1B, QFBE, DCH2B, B2E, DCH3B, MIPBE, DCH3B, B1E,&
             DB1AE, BPM, DB1B, QDXBE, DB2A, HVCOR, DB2B,&
             QFXBE, DB3A, BPM, DB3B, QDYBE, DM4, B34, DB5A, BPM, DB5B,& 
             SDI, DB6A, HVCOR, DB6B, SFI, DM7, QFYBE, DB7A, BPM,&
             DB7B, SFI, DB6C, HVCOR, DB6D, SDI, DB5C, BPM, DB5D, B34,&  
             DB8A, BPM, DB8B, QDZBE, DB9A, HVCOR, DB9B, QFZBE, DB10A, BPM,&
             DB10B, MME, DB10C)

 CEL10: LINE=(QDE, DCH1A, QFAE, DCH2A, B3E, DCH3A, MIPAE, DCH3A, B4E,&
              DA1AE, BPM, DA1B, QDXAE, DA2A, HVCOR, DA2B,&
              QFXAE, DA3A, BPM, DA3B, QDYAE, DM4, B34, DA5A, BPM, DA5B,&
              SDI, DA6A, HVCOR, DA6B, SFI, DA7A ,BPM ,DA7B ,QFYAE,&
              DM7 ,SFI ,DA6C, HVCOR, DA6D, SDI, DA5C, BPM, DA5D, B34,&
              DA8A, BPM, DA8B, QDZAE, DA9A, HVCOR, DA9B, QFZAE, DA10A, BPM,&
              DA10B, MME, DA10C)

! Full ring
! note: BL13 is inside CEL18

 RING: LINE= (MRF,CEL1,      CEL2, M3m,CEL3, MMM, CEL4, M3m,CEL5, BL7,&
                  CEL6, BL10,CEL7, BL9,CEL8,     -CEL9,&
                  CEL10,     CEL11,BL6,CEL12,BL5, CEL13,M3m,CEL14,M3m,&
                  CEL15,BL11,CEL16,BL4,CEL17,    -CEL18)

 RETURN
