! 04/03/04 Eugene: added RF and Kickers
! 21/05/04 Eugene: Lengths of D1 changed to get circumference to 216 (up to 1 micron)
! 27/07/04 Eugene: Added BPMs and girder markers for misalignment studies.
TITLE
ASP STORAGE RING

UTRANSPORT
USTANDARD
PI := 3.14159265358979

ECHO


! ### BPMs and Girder markers ###
BPM: MONITOR, L=0
! For girders 1 and 3
GM1A: MARKER
GM1B: MARKER
! For girders 2 (centre)
GM2A: MARKER
GM2B: MARKER
! For the dipoles
GMDA: MARKER
GMDB: MARKER


! ### DRIFTS ###
D1: DRIFT, L=2.698286
D2: DRIFT, L=0.1900
D3: DRIFT, L=0.1650
D4: DRIFT, L=0.2750
D5: DRIFT, L=0.1550
D6: DRIFT, L=0.4500
! Drifts around BPMs
D1A: DRIFT, L=2.304
D1B: DRIFT, L=D1[L]-D1A[L]
D4A: DRIFT, L=0.076
D4B: DRIFT, L=D4[L]-D4A[L]
D4AA: DRIFT, L=0.211
D4BB: DRIFT, L=D4[L]-D4AA[L]
D2A: DRIFT, L=0.087
D2B: DRIFT, L=D2[L]-D2A[L]


! ### Dipole ###
! Single dipole. For ANY changes ONLY alter "DIP".
DLEN   := 1.726
DANGLE := (360/28)*(pi/180)
DK1    := 19.819*(-DANGLE*DANGLE/DLEN/DLEN)
DIP: RBEND, L=DLEN, ANGLE=DANGLE, K1=DK1
! Symmetric split of the dipole into six parts
DIPI: SBEND, L=DIP[L]/6, ANGLE=DANGLE/6, E1=DANGLE/2, E2=0, K1=DIP[K1]
DIPM: SBEND, L=DIP[L]/6, ANGLE=DANGLE/6, E1=0, E2=0, K1=DIP[K1]
DIPO: SBEND, L=DIP[L]/6, ANGLE=DANGLE/6, E1=0, E2=DANGLE/2, K1=DIP[K1]
DSPLIT: LINE=(DIPI,4*DIPM,DIPO)
! Split dipole at the source points
DIPS3: SBEND, L=DIP[L]*0.46, ANGLE=DANGLE*0.46, E1=DANGLE/2, E2=0, K1=DIP[K1]
DIPS4: SBEND, L=DIP[L]*0.54, ANGLE=DANGLE*0.54, E1=0, E2=DANGLE/2, K1=DIP[K1]
DIPS1: SBEND, L=DIP[L]*0.544, ANGLE=DANGLE*0.544, E1=DANGLE/2, E2=0, K1=DIP[K1]
DIPS2: SBEND, L=DIP[L]*0.456, ANGLE=DANGLE*0.456, E1=0, E2=DANGLE/2, K1=DIP[K1]

! Select which set of dipoles to use here, SINGLE, SPLIT or by source points.
!DIP1:LINE=(GMDA, DIPS1,DIPS2, GMDB)
!DIP2:LINE=(GMDA, DIPS3,DIPS4, GMDB)
DIP1: LINE=(GMDA, DIP, GMDB)
DIP2: LINE=(GMDA, DIP, GMDB)
!DIP1: LINE=(GMDA, DSPLIT, GMDB)
!DIP2: LINE=(GMDA, DSPLIT, GMDB)

! MEASURED
CALL, FILENAME ="newDipoleRBEND.dimad"


! ### QUADRUPOLES ###
Q1SINGLE: QUADRUPOLE, L=0.3550, K1=1.7600
Q2SINGLE: QUADRUPOLE, L=0.1800, K1=-1.0400
Q3SINGLE: QUADRUPOLE, L=0.3550, K1=1.5300
Q14: Q1SINGLE, L=Q1SINGLE[L]/4
Q24: Q2SINGLE, L=Q2SINGLE[L]/4
Q34: Q3SINGLE, L=Q3SINGLE[L]/4

! Select which quadrupole to use
Q1: LINE=(4*Q14)
Q2: LINE=(4*Q24)
Q3: LINE=(4*Q34)
!Q1: Q1SINGLE
!Q2: Q2SINGLE
!Q3: Q3SINGLE


! ### SEXTUPOLES ###
! Uncomment the sextupoles that you want to use
!S1: SEXTUPOLE, L=0.1000, K2=14.0000
!S2: S1, K2=-15.0000
!S3: S1, K2=-6.5000
!S4: S1, K2=7.1000
S1: SEXTUPOLE, L=0.1000, K2=14.0000*2
S2: S1, K2=-14.0000*2
S3: S1, K2=-6.5000*2
S4: S1, K2=7.1000*2
!S1: SEXTUPOLE, L=0.1000, K2=0
!S2: S1, K2=0
!S3: S1, K2=0
!S4: S1, K2=0


! ### CORRECTORS ###
HCOR: GKICK, DXP=0
VCOR: GKICK, DYP=0
! For studying local bumps
HCOR11: GKICK, DXP=0
HCOR21: GKICK, DXP=0
HCOR31: GKICK, DXP=0
HCOR12: GKICK, DXP=0
HCOR22: GKICK, DXP=0
HCOR32: GKICK, DXP=0
HCOR13: GKICK, DXP=0
HCOR23: GKICK, DXP=0
HCOR33: GKICK, DXP=0
VCOR11: GKICK, DYP=0
VCOR21: GKICK, DYP=0
VCOR31: GKICK, DYP=0
VCOR41: GKICK, DYP=0
VCOR12: GKICK, DYP=0
VCOR22: GKICK, DYP=0
VCOR32: GKICK, DYP=0
VCOR42: GKICK, DYP=0
VCOR13: GKICK, DYP=0
VCOR23: GKICK, DYP=0
VCOR33: GKICK, DYP=0
VCOR43: GKICK, DYP=0


! ### RF ###
RF: RFCAVITY, L=0.0, VOLT=3000, LAG=-0.215, FREQ=500, ENERGY=3
! Associated drifts thats placed on either side of the cavity
D11: DRIFT, L=D1[L]-RF[L]/2
D22: DRIFT, L=D1[L]-RF[L]/2


! ### Kicker magnets ###
KICK1: GKICK, DXP=0.00223
KICK2: GKICK, DXP=-0.00185
KICK3: GKICK, DXP=-0.00185
KICK4: GKICK, DXP=0.00223
! Drifts on either side of the kickers to put it in the correct position.
! Remove .3 drift (position of one end of kicker) and half a kicker length .7/2
! Orig -> DK11: DRIFT, L=2.698286-0.3-0.7/2
! Orig -> DK12: DRIFT, L=0.3+0.7/2
DK11: DRIFT, L=2.698286-0.384714
DK12: DRIFT, L=0.384714
DK21: DRIFT, L=2.698286-0.678714
DK22: DRIFT, L=0.678714
DK31: DRIFT, L=2.698286-0.384714
DK32: DRIFT, L=0.384714
DK41: DRIFT, L=2.698286-0.678714
DK42: DRIFT, L=0.678714



! ### DEFINE SOME BASIC SECTORS ###
! BPM elements each will replace the drift as a whole
BPM1D1: LINE=(D1A,BPM,D1B)
BPM2D4: LINE=(D4A,BPM,D4B)
BPM3D4: LINE=(D4AA,BPM,D4BB)
BPM4D2: LINE=(D2A,BPM,D2B)
BPM5D4: LINE=(BPM2D4)
BPM6D4: LINE=(BPM3D4)
BPM7D1: LINE=(D1B,BPM,D1A)
! Separate groups of elements into girders. The dipoles are stand alone elements.
GIRDER1: LINE=(GM1A, S1,HCOR,S1,D2,Q1,D3,S2,VCOR,S2, GM1B)
GIRDER2: LINE=(GM1A, S3,VCOR,S3,D5,Q2,D6,Q3,D2,S4,HCOR,S4,BPM4D2,Q3,D6,&
               Q2,D5,S3,VCOR,S3, GM1B)
GIRDER3: LINE=(GM1A, S2,VCOR,S2,D3,Q1,D2,S1,HCOR,S1, GM1B)

! Basic ARC and straight sections
CEL: LINE=(BPM1D1,GIRDER1,BPM2D4,DIP1,BPM3D4,GIRDER2,BPM5D4,DIP2,BPM6D4,&
           GIRDER3,BPM7D1)

NCEL: LINE=(BPM1D1,GIRDER1,NEWBEND,GIRDER2,NEWBEND,&
           GIRDER3,BPM7D1)
            
! For creating local bumps.
CEL1: LINE=(D1,S1,HCOR11,S1,D2,Q1,D3,S2,VCOR11,S2,D4,DIP1,D4,S3,VCOR21,S3,&
             D5,Q2,D6,Q3,D2,S4,HCOR21,S4,D2,Q3,D6,Q2,D5,S3,VCOR31,S3,D4,&
             DIP2,D4,S2,VCOR41,S2,D3,Q1,D2,S1,HCOR31,S1,D1)
CEL2: LINE=(D1,S1,HCOR12,S1,D2,Q1,D3,S2,VCOR12,S2,D4,DIP1,D4,S3,VCOR22,S3,&
             D5,Q2,D6,Q3,D2,S4,HCOR22,S4,D2,Q3,D6,Q2,D5,S3,VCOR32,S3,D4,&
             DIP2,D4,S2,VCOR42,S2,D3,Q1,D2,S1,HCOR32,S1,D1)
CEL3: LINE=(D1,S1,HCOR13,S1,D2,Q1,D3,S2,VCOR13,S2,D4,DIP1,D4,S3,VCOR23,S3,&
             D5,Q2,D6,Q3,D2,S4,HCOR23,S4,D2,Q3,D6,Q2,D5,S3,VCOR33,S3,D4,&
             DIP2,D4,S2,VCOR43,S2,D3,Q1,D2,S1,HCOR33,S1,D1)
             
! For the insertion of an RF cavity.
CELRF06: LINE=(D1,S1,HCOR,S1,D2,Q1,D3,S2,VCOR,S2,D4,DIP1,D4,S3,VCOR,S3,D5,Q2,&
            D6,Q3,D2,S4,HCOR,S4,D2,Q3,D6,Q2,D5,S3,VCOR,S3,D4,DIP2,D4,&
            S2,VCOR,S2,D3,Q1,D2,S1,HCOR,S1,D11)
CELRF07: LINE=(D11,S1,HCOR,S1,D2,Q1,D3,S2,VCOR,S2,D4,DIP1,D4,S3,VCOR,S3,D5,Q2,&
            D6,Q3,D2,S4,HCOR,S4,D2,Q3,D6,Q2,D5,S3,VCOR,S3,D4,DIP2,D4,&
            S2,VCOR,S2,D3,Q1,D2,S1,HCOR,S1,D1)
TESTRF: LINE=(D22,S1,HCOR,S1,D2,Q1,D3,S2,VCOR,S2,D4,DIP1,D4,S3,VCOR,S3,D5,Q2,&
            D6,Q3,D2,S4,HCOR,S4,D2,Q3,D6,Q2,D5,S3,VCOR,S3,D4,DIP2,D4,&
            S2,VCOR,S2,D3,Q1,D2,S1,HCOR,S1,D1)
            
! For kicker magnets around the injection straights.
CELKICK14: LINE=(DK11,KICK1,DK12,&
            S1,HCOR,S1,D2,Q1,D3,S2,VCOR,S2,NEWBEND,S3,VCOR,S3,D5,Q2,&
            D6,Q3,D2,S4,HCOR,S4,D2,Q3,D6,Q2,D5,S3,VCOR,S3,NEWBEND,&
            S2,VCOR,S2,D3,Q1,D2,S1,HCOR,S1,DK22,KICK2,DK21)            
CELKICK01: LINE=(DK31,KICK3,DK32,&
            S1,HCOR,S1,D2,Q1,D3,S2,VCOR,S2,NEWBEND,S3,VCOR,S3,D5,Q2,&
            D6,Q3,D2,S4,HCOR,S4,D2,Q3,D6,Q2,D5,S3,VCOR,S3,NEWBEND,&
            S2,VCOR,S2,D3,Q1,D2,S1,HCOR,S1,DK42,KICK4,DK41)


! ### DEFINE BEAMLINES/RINGS ###
! For creating local bumps without RF and kickers.
BUMPSECT: LINE=(CEL1,CEL2)
BUMPRING: LINE=(6*CEL,CEL1,CEL2,CEL3,5*CEL)

! For creating local bumps WITH RF and kickers.
KRRF: LINE=(CELRF06, RF, CELRF07, 4*CEL,CEL1,CEL2,CEL3,5*CEL)

! Ring with just the RF
RFRING: LINE=(CELRF06, RF, CELRF07, 12*CEL)
TRF: LINE=(RF, TESTRF, 13*CEL)

! Ordinary storage ring
RING: LINE=(14*CEL)
NEWRING: LINE=(14*NCEL)
NEWKRING: LINE=(CELKICK14, CELKICK01, 12*NCEL)


! Ring complete with RF and kicker magnets.
FULLRING: LINE=(CELKICK14, CELKICK01, 4*CEL, CELRF06, RF, CELRF07,&
                6*CEL)

TESTLINE( Q1 , Q2 ): LINE=(Q1,Q2,DIP1)
TT: LINE=(2*TESTLINE(Q14,Q24))

USE, NEWKRING
! USE, RING
DIMAT
OUTPUT
4,
MODIFY TUNES [0.3 0.2], DISPERSION 0.0 AND CHROMATICITY [0 0] with s1=14 and s2=-14
5
Q14 K1 0.17617410683215E+01
Q24 K1 -0.10383770570250E+01
Q34 K1 0.15338016353322E+01
S3 K2 -14.02926931581820
S4 K2 14.37869143146240,
! With new bends and quads fitted to regain original optical functions
MODIFY TUNES [0.3 0.2], DISPERSION 0.0 AND CHROMATICITY [0 0] with for NEWRING
5
Q14 K1 1.76273060226681
Q24 K1 -1.09154032397246
Q34 K1 1.54553247963630
S3 K2 -14.02926931581820
S4 K2 14.37869143146240,
MODIFY TUNES [0.3 0.2], DISPERSION 0.0 AND CHROMATICITY [0 0] with for NEWRING
5
Q14 K1 1.7610967
Q24 K1 -1.0715748
Q34 K1 1.5406418
S3 K2 -14.02926931581820
S4 K2 14.37869143146240,
MATR
2 -1,
MACHINE
3.0 3.05 .05 2 0 1 360
0 0 0 0
0 0 0 0 0,
MOVEment
0 1 0 -1 1 1 0.001
0 0 0 0 0 0
0;
STOP


SET SYMPLECTIC
1 3.0,
TRACK two particle with different energy offsets
-1 1 2 2000
1e-6 0 1e-6 0 0 0
1e-6 0 1e-6 0 0 0.001
0;
STOP

MACHINE
3.0 3.05 .05 2 0 1 360
0 0 0 0
0 0 0 0 0,
CHROmatic precision option
1,
MATR
2 -1,
MACHINE
3.0 3.05 .05 2 0 1 360
0 0 0 0
0 0 0 0 0
STOP
